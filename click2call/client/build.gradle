
task copySources(type: Copy) {
    filter { String line -> line.replace("<reference path=\"communicator.ts\" />", "<reference path=\"a3.d.ts\" />") }
    include('*.ts')
    from('src')
    into(buildDir)  
}

task copyLibs(type: Copy) {
    dependsOn ':libraries:a3client:build'
    include 'a3.*'
    include '*.swf'
    from('../../libraries/a3client/build')
    into(buildDir)
}


task (build, type: Exec) {
    dependsOn 'copySources'
    dependsOn 'copyLibs'

    doFirst {
        workingDir buildDir
        def commandLineArgs = ['tsc', '-d', '--sourcemap', 'main.ts', '--target', 'ES5', '--outDir', buildDir, '--out', 'main.js'];
        def os = System.getProperty("os.name").toLowerCase();
        if(os.contains('mac')){
            commandLineArgs[0] = "/usr/local/bin/tsc";
        }else if(os.contains('windows')){
            commandLineArgs[0] = System.getenv()['APPDATA'] + '\\npm\\node_modules\\typescript\\bin\\tsc'
            commandLineArgs.add(0, 'node');
        }
        commandLine commandLineArgs
    }
}

task (dist, type: Tar) {
    dependsOn 'build'
    extension = 'tar.gz'
    baseName = 'v2chat-client-dist'
    compression = Compression.GZIP
    exclude '**/.svn'
    exclude '**/.git'

    doFirst {
        def info = 'BUILD INFO:\n';
            info+= "Version:     ${version}\n";
            info+= "Revision:    ${project.ext.revision}\n";
            info+= "Builder:     ${project.ext.builder}\n";
            info+= "Date:        ${project.ext.datetime}\n";
        file('build/build.info').write(info);
    }

    from(buildDir){
        include 'build.info'
        into('www')
    }

    from(buildDir){
        include '*.swf'
        include 'a3.js'
        include 'main.js'
        into('www/a3')
    }

    from('src'){
        include 'slider/*'
        include '*.png'
        include 'click2call.js'
        include 'libs.js'
        into('www/a3')
    }

    from('src'){
        filter { String line -> line.replace("localhost", "webrtc.v2chat.com") }
        include 'click2call.html'
        include 'editor.html'
        into('www/a3')
    }

    from('resources/help'){
        into('www/help')
    }

    from('resources/template'){
        into('www/tmpl/default')
    }
}

